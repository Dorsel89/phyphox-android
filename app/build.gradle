apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "de.rwth_aachen.phyphox"
        minSdkVersion 14
        targetSdkVersion 31

        versionName "1.1.10"
        //  format  WXXYYZZ, where WW is major, XX is minor, YY is patch, and ZZ is build
        versionCode 1011002 //1.01.10-02

        resConfigs "en", "cs", "de", "el", "es", "fr", "it", "ja", "nl", "pl", "pt", "ru", "sr", "b+sr+Latn", "tr", "vi", "zh-rCN", "zh-rTW"
        vectorDrawables {
            useSupportLibrary = true
        }
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    buildTypes {
        release {
            lintOptions {
                disable 'MissingTranslation'
            }
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        cmake {
            path 'src/main/cpp/CMakeLists.txt'
        }
    }

    flavorDimensions "permissions"
    productFlavors {
        screenshot {
            dimension "permissions"
            minSdkVersion 18
        }
        regular {
            dimension "permissions"
        }
    }

    compileOptions {
        encoding = 'UTF-8'
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    useLibrary 'org.apache.http.legacy'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.annotation:annotation:1.3.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.appcompat:appcompat-resources:1.3.1'
    implementation 'androidx.core:core:1.7.0'
    implementation 'androidx.fragment:fragment:1.3.6'
    implementation 'androidx.viewpager:viewpager:1.0.0'

    //https://github.com/journeyapps/zxing-android-embedded/blob/master/CHANGES.md
    implementation 'com.journeyapps:zxing-android-embedded:3.5.0'

    //https://poi.apache.org/changes.html
    implementation 'org.apache.poi:poi:3.13'

    //https://bigbadaboom.github.io/androidsvg/release_notes.html
    implementation 'com.caverock:androidsvg:1.4'

    //Automated screenshot generation
    androidTestImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'tools.fastlane:screengrab:2.0.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    //https://github.com/eclipse/paho.mqtt.android
    implementation "org.eclipse.paho:org.eclipse.paho.client.mqttv3:${rootProject.ext.clientVersion}"
    implementation project(":org.eclipse.paho.android.service")
}
